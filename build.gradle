buildscript {

    ext {
        androidPlugin = 'com.android.tools.build:gradle:3.0.1'
        minSdkVersion = 14
        compileSdkVersion = 27
        targetSdkVersion = 27

        mavenPlugin = 'com.github.dcendents:android-maven-gradle-plugin:2.0'

        materialDialogsVersion = '0.9.6.0'
        materialDialogsCore = "com.afollestad.material-dialogs:core:$materialDialogsVersion"
        materialDialogsCommons = "com.afollestad.material-dialogs:commons:$materialDialogsVersion"

        supportVersion = '27.0.2'
        supportAppCompat = "com.android.support:appcompat-v7:$supportVersion"

        rxJava = 'io.reactivex.rxjava2:rxjava:2.1.8'
    }
    
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath rootProject.ext.androidPlugin
        classpath rootProject.ext.mavenPlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects { project ->
    project.configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.android.support') {
                    details.useVersion(rootProject.ext.supportVersion)
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
